struct ScharrOperators
{
    float3x3 x;
    float3x3 y;
};

ScharrOperators GetEdgeDetectionKernels()
{
    ScharrOperators k;

    k.x = float3x3(
         3,  0, -3,
        10,  0,-10,
         3,  0, -3
    );

    k.y = float3x3(
         3, 10,  3,
         0,  0,  0,
        -3,-10, -3
    );

    return k;
}

void DepthBasedOutlines_float(float2 screenUV, float2 px, out float outlines)
{
    outlines = 0.0;

#if defined(UNITY_DECLARE_DEPTH_TEXTURE_INCLUDED)
    ScharrOperators kernels = GetEdgeDetectionKernels();

    float gx = 0.0;
    float gy = 0.0;

    for (int i = -1; i <= 1; i++)
    {
        for (int j = -1; j <= 1; j++)
        {
            float2 offset = float2(i, j) * px;
            float d = SampleSceneDepth(screenUV + offset);

            gx += d * kernels.x[i + 1][j + 1];
            gy += d * kernels.y[i + 1][j + 1];
        }
    }

    float g = sqrt(gx * gx + gy * gy);
    outlines = step(0.02, g);
#endif
}

void NormalBasedOutlines_float(float2 screenUV, float2 px, out float outlines)
{
    outlines = 0.0;

    #if defined(UNITY_DECLARE_NORMALS_TEXTURE_INCLUDED)
    ScharrOperators kernels = GetEdgeDetectionKernels();

    float gx = 0.0;
    float gy = 0.0;
    float3 cn = SampleSceneNormals(screenUV);
    
    for (int i = -1; i <= 1; i++)
    {
        for (int j = -1; j <= 1; j++)
        {
            float2 offset = float2(i, j) * px;
            float n = SampleSceneNormals(screenUV + offset);
            float dp = dot(cn, n);

            gx += dp * kernels.x[i + 1][j + 1];
            gy += dp * kernels.y[i + 1][j + 1];
        }
    }

    float g = sqrt(gx * gx + gy * gy);
    outlines = step(2, g);
    #endif
}